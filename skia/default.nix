{ stdenv, lib, fetchFromGitHub, fetchgit, python3, gn, ninja
, fontconfig, expat, icu, libglvnd, libjpeg, libpng, libwebp, zlib
, mesa, libX11, harfbuzzFull
}:

let
  # skia-deps.nix is generated by: ./skia-make-deps.sh 'angle2|dng_sdk|piex|sfntly|wuffs'
  depSrcs = import ./skia-deps.nix { inherit fetchgit; };
in
stdenv.mkDerivation {
  pname = "skia";
  version = "chrome/m123";

  src = fetchFromGitHub {
    owner = "google";
    repo = "skia";
    rev = "chrome/m123";
    hash = "sha256-UF0GRvn3DHvsLcOLE6UvW775jj2zpnnMBOlRw0k/584=";
  };

  nativeBuildInputs = [ python3 gn ninja ];

  buildInputs = [
    fontconfig expat icu libglvnd libjpeg libpng libwebp zlib
    mesa libX11 harfbuzzFull
  ];

  preConfigure = ''
    mkdir -p third_party/externals
    ln -s ${depSrcs.angle2} third_party/externals/angle2
    ln -s ${depSrcs.dng_sdk} third_party/externals/dng_sdk
    ln -s ${depSrcs.piex} third_party/externals/piex
    ln -s ${depSrcs.sfntly} third_party/externals/sfntly
    ln -s ${depSrcs.wuffs} third_party/externals/wuffs
  '';

  configurePhase = ''
    runHook preConfigure
    gn gen out/Release --args="is_debug=false is_official_build=true extra_cflags=[\"-I${harfbuzzFull.dev}/include/harfbuzz\"]"
    runHook postConfigure
  '';

  buildPhase = ''
    runHook preBuild
    ninja -C out/Release skia modules
    runHook postBuild
  '';

  installPhase = ''
    mkdir -p $out/lib $out/include

    # Glob will match all subdirs.
    shopt -s globstar

    cp -r --parents -t $out/include/ \
      include/codec \
      include/config \
      include/core \
      include/effects \
      include/gpu \
      include/private \
      include/utils \
      include/third_party/skcms/*.h \
      src/gpu/**/*.h \
      src/core/*.h \
      modules/skshaper/include/*.h \
      third_party/externals/angle2/include \
      third_party/skcms/**/*.h

    cp out/Release/*.a $out/lib/
  '';

  meta = with lib; {
    description = "Skia is a complete 2D graphic library for drawing Text, Geometries, and Images";
    homepage = "https://skia.org/";
    license = licenses.bsd3;
    platforms = platforms.all;
  };
}
